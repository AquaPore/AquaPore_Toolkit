{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# EXAMPLES ANIMATIONS"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GLMakie.Screen(...)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using GLMakie\n",
    "\n",
    "figure = Figure()\n",
    "\n",
    "# create two sliders\n",
    "slider1, slider2 =\n",
    "    Makie.SliderGrid(\n",
    "        figure[2, 1],\n",
    "        (; label = \"Slider 1\", range = -1:0.01:1),\n",
    "        (; label = \"Slider 2\", range = -1:0.01:1),\n",
    "    ).sliders\n",
    "\n",
    "\t \n",
    "obs_func = on(slider1.value) do val\n",
    "\tabs(val+slider2.value[])>0.01 && Makie.set_close_to!(slider2, -val)\n",
    "end\n",
    "\n",
    "obs_func2 = on(slider2.value) do val\n",
    "\tabs(slider1.value[]+val)>0.01 && Makie.set_close_to!(slider1, -val)\n",
    "end\n",
    "\n",
    "# create a slimple scatter point based on the slider (just a dummy for the purpose of this example)\n",
    "data = @lift [Makie.Point2f($(slider1.value), $(slider2.value))]\n",
    "scatter(figure[1, 1], data)\n",
    "\n",
    "display(figure)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "\n",
    "f = Figure()\n",
    "Axis(f[1, 1], limits = (0, 1, 0, 1))\n",
    "\n",
    "rs_h = IntervalSlider(f[2, 1], range = LinRange(0, 1, 1000),\n",
    "    startvalues = (0.2, 0.8))\n",
    "\n",
    "rs_v = IntervalSlider(f[1, 2], range = LinRange(0, 1, 1000),\n",
    "    startvalues = (0.4, 0.9), horizontal = false)\n",
    "\n",
    "labeltext1 = lift(rs_h.interval) do int\n",
    "    string(round.(int, digits = 2))\n",
    "end\n",
    "\n",
    "Label(f[3, 1], labeltext1, tellwidth = false)\n",
    "labeltext2 = lift(rs_v.interval) do int\n",
    "    string(round.(int, digits = 2))\n",
    "end\n",
    "Label(f[1, 3], labeltext2,\n",
    "    tellheight = false, rotation = pi/2)\n",
    "\n",
    "points = rand(Point2f, 300)\n",
    "\n",
    "# color points differently if they are within the two intervals\n",
    "colors = lift(rs_h.interval, rs_v.interval) do h_int, v_int\n",
    "    map(points) do p\n",
    "        (h_int[1] < p[1] < h_int[2]) && (v_int[1] < p[2] < v_int[2])\n",
    "    end\n",
    "end\n",
    "\n",
    "scatter!(points, color = colors, colormap = [:gray90, :dodgerblue], strokewidth = 0)\n",
    "\n",
    "f\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "fig = Figure()\n",
    "\n",
    "ax = Axis(fig[1, 1])\n",
    "\n",
    "toggles = [Toggle(fig, active = active) for active in [true, false]]\n",
    "labels = [Label(fig, lift(x -> x ? \"$l visible\" : \"$l invisible\", t.active))\n",
    "    for (t, l) in zip(toggles, [\"sine\", \"cosine\"])]\n",
    "\n",
    "fig[1, 2] = grid!(hcat(toggles, labels), tellheight = false)\n",
    "\n",
    "line1 = lines!(0..10, sin, color = :blue, visible = false)\n",
    "line2 = lines!(0..10, cos, color = :red)\n",
    "\n",
    "connect!(line1.visible, toggles[1].active)\n",
    "connect!(line2.visible, toggles[2].active)\n",
    "\n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "\n",
    "fig = Figure()\n",
    "\n",
    "ax = Axis(fig[1, 1])\n",
    "\n",
    "sg = SliderGrid(\n",
    "    fig[1, 2],\n",
    "    (label = \"Voltage\", range = 0:0.1:10, format = \"{:.1f}V\", startvalue = 5.3),\n",
    "    (label = \"Current\", range = 0:0.1:20, format = \"{:.1f}A\", startvalue = 10.2),\n",
    "    (label = \"Resistance\", range = 0:0.1:30, format = \"{:.1f}Ω\", startvalue = 15.9),\n",
    "    width = 350,\n",
    "    tellheight = false)\n",
    "\n",
    "sliderobservables = [s.value for s in sg.sliders]\n",
    "bars = lift(sliderobservables...) do slvalues...\n",
    "    [slvalues...]\n",
    "end\n",
    "\n",
    "barplot!(ax, bars, color = [:yellow, :orange, :red])\n",
    "ylims!(ax, 0, 30)\n",
    "\n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "\n",
    "fig = Figure()\n",
    "\n",
    "ax = Axis(fig[1, 1])\n",
    "fig[2, 1] = buttongrid = GridLayout(tellwidth = false)\n",
    "\n",
    "counts = Observable([1, 4, 3, 7, 2])\n",
    "\n",
    "buttonlabels = [lift(x -> \"Count: $(x[i])\", counts) for i in 1:5]\n",
    "\n",
    "buttons = buttongrid[1, 1:5] = [Button(fig, label = l) for l in buttonlabels]\n",
    "\n",
    "for i in 1:5\n",
    "    on(buttons[i].clicks) do n\n",
    "        counts[][i] += 1\n",
    "        notify(counts)\n",
    "    end\n",
    "end\n",
    "\n",
    "barplot!(counts, color = cgrad(:Spectral)[LinRange(0, 1, 5)])\n",
    "ylims!(ax, 0, 20)\n",
    "\n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "fig = Figure()\n",
    "\n",
    "menu = Menu(fig, options = [\"viridis\", \"heat\", \"blues\"], default = \"blues\")\n",
    "\n",
    "funcs = [sqrt, x->x^2, sin, cos]\n",
    "\n",
    "menu2 = Menu(fig,\n",
    "    options = zip([\"Square Root\", \"Square\", \"Sine\", \"Cosine\"], funcs),\n",
    "    default = \"Square\")\n",
    "\n",
    "fig[1, 1] = vgrid!(\n",
    "    Label(fig, \"Colormap\", width = nothing),\n",
    "    menu,\n",
    "    Label(fig, \"Function\", width = nothing),\n",
    "    menu2;\n",
    "    tellheight = false, width = 200)\n",
    "\n",
    "ax = Axis(fig[1, 2])\n",
    "\n",
    "func = Observable{Any}(funcs[1])\n",
    "\n",
    "ys = lift(func) do f\n",
    "    f.(0:0.3:10)\n",
    "end\n",
    "scat = scatter!(ax, ys, markersize = 10px, color = ys)\n",
    "\n",
    "cb = Colorbar(fig[1, 3], scat)\n",
    "\n",
    "on(menu.selection) do s\n",
    "    scat.colormap = s\n",
    "end\n",
    "notify(menu.selection)\n",
    "\n",
    "on(menu2.selection) do s\n",
    "    func[] = s\n",
    "    autolimits!(ax)\n",
    "end\n",
    "notify(menu2.selection)\n",
    "\n",
    "menu2.is_open = true\n",
    "\n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "ename": "ArgumentError",
     "evalue": "ArgumentError: Package GraphMakie not found in current path.\n- Run `import Pkg; Pkg.add(\"GraphMakie\")` to install the GraphMakie package.",
     "output_type": "error",
     "traceback": [
      "ArgumentError: Package GraphMakie not found in current path.\n",
      "- Run `import Pkg; Pkg.add(\"GraphMakie\")` to install the GraphMakie package.\n",
      "\n",
      "Stacktrace:\n",
      "  [1] macro expansion\n",
      "    @ .\\loading.jl:2223 [inlined]\n",
      "  [2] macro expansion\n",
      "    @ .\\lock.jl:273 [inlined]\n",
      "  [3] __require(into::Module, mod::Symbol)\n",
      "    @ Base .\\loading.jl:2198\n",
      "  [4] #invoke_in_world#3\n",
      "    @ .\\essentials.jl:1089 [inlined]\n",
      "  [5] invoke_in_world\n",
      "    @ .\\essentials.jl:1086 [inlined]\n",
      "  [6] require(into::Module, mod::Symbol)\n",
      "    @ Base .\\loading.jl:2191\n",
      "  [7] eval\n",
      "    @ .\\boot.jl:430 [inlined]\n",
      "  [8] include_string(mapexpr::typeof(REPL.softscope), mod::Module, code::String, filename::String)\n",
      "    @ Base .\\loading.jl:2643\n",
      "  [9] #invokelatest#2\n",
      "    @ .\\essentials.jl:1055 [inlined]\n",
      " [10] invokelatest\n",
      "    @ .\\essentials.jl:1052 [inlined]\n",
      " [11] (::VSCodeServer.var\"#217#218\"{VSCodeServer.NotebookRunCellArguments, String})()\n",
      "    @ VSCodeServer c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\packages\\VSCodeServer\\src\\serve_notebook.jl:24\n",
      " [12] withpath(f::VSCodeServer.var\"#217#218\"{VSCodeServer.NotebookRunCellArguments, String}, path::String)\n",
      "    @ VSCodeServer c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\packages\\VSCodeServer\\src\\repl.jl:276\n",
      " [13] notebook_runcell_request(conn::VSCodeServer.JSONRPC.JSONRPCEndpoint{Base.PipeEndpoint, Base.PipeEndpoint}, params::VSCodeServer.NotebookRunCellArguments)\n",
      "    @ VSCodeServer c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\packages\\VSCodeServer\\src\\serve_notebook.jl:13\n",
      " [14] dispatch_msg(x::VSCodeServer.JSONRPC.JSONRPCEndpoint{Base.PipeEndpoint, Base.PipeEndpoint}, dispatcher::VSCodeServer.JSONRPC.MsgDispatcher, msg::Dict{String, Any})\n",
      "    @ VSCodeServer.JSONRPC c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\packages\\JSONRPC\\src\\typed.jl:67\n",
      " [15] serve_notebook(pipename::String, debugger_pipename::String, outputchannel_logger::Base.CoreLogging.SimpleLogger; error_handler::var\"#5#10\"{String})\n",
      "    @ VSCodeServer c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\packages\\VSCodeServer\\src\\serve_notebook.jl:147\n",
      " [16] top-level scope\n",
      "    @ c:\\Users\\PollaccoJ\\.vscode\\extensions\\julialang.language-julia-1.124.2\\scripts\\notebook\\notebook.jl:35"
     ]
    }
   ],
   "source": [
    "using GLMakie, GraphMakie, Graphs, Graphs.SimpleGraphs, Makie, Observables\n",
    "import NetworkLayout\n",
    "GLMakie.activate!()\n",
    "\n",
    "function modify_element!(x, visible, width, height)\n",
    "    x.blockscene.visible[] = visible\n",
    "    x.width[] = width\n",
    "    x.height[] = height\n",
    "end\n",
    "hide_element!(x) = modify_element!(x, false, Fixed(0), Fixed(0))\n",
    "hide_slider!(x) = hide_element!(x)\n",
    "hide_label!(x)  = hide_element!(x)\n",
    "show_slider!(x) = modify_element!(x, true, 300,     Auto(0)) # width matches initialize_sliders!\n",
    "show_label!(x)  = modify_element!(x, true, Auto(0), Auto(0))\n",
    "\n",
    "function show_slidergrid!(slidergrid)\n",
    "    show_slider!( slidergrid)             # show the selected slidergrid\n",
    "    show_slider!.(slidergrid.sliders)     # show the sliders within the selected grid\n",
    "    show_label!.( slidergrid.labels)      # show the labels  within the selected grid\n",
    "    show_label!.( slidergrid.valuelabels) # hides the right-labels of the non-selected grid\n",
    "end\n",
    "\n",
    "function hide_slidergrid!(slidergrid)\n",
    "    hide_slider!( slidergrid)             # hides the non-selected slidergrid\n",
    "    hide_slider!.(slidergrid.sliders)     # hides the sliders of the non-selected grid\n",
    "    hide_label!.( slidergrid.labels)      # hides the left-labels  of the non-selected grid\n",
    "    hide_label!.( slidergrid.valuelabels) # hides the right-labels of the non-selected grid\n",
    "end\n",
    "\n",
    "function toggle_slidergrid!(slidervec, i)\n",
    "\n",
    "    show_slidergrid!(slidervec[i])\n",
    "    for j in 1:i-1\n",
    "        hide_slidergrid!(slidervec[j])\n",
    "    end\n",
    "    for j in i+1:length(slidervec)\n",
    "        hide_slidergrid!(slidervec[j])\n",
    "    end\n",
    "end\n",
    "\n",
    "function initialize_sliders!(fig)\n",
    "    SliderGrid(\n",
    "        fig[2, 1],\n",
    "        (label = \"n\", range = 3:7,     format = \"{:d}\",   startvalue = 3),\n",
    "        (label = \"p\", range = 0:.05:1, format = \"{:.2f}\", startvalue = .1),\n",
    "        width = 250,\n",
    "        halign = :left,\n",
    "        tellheight = false\n",
    "    )\n",
    "    SliderGrid(\n",
    "        fig[2, 1],\n",
    "        (label = \"n\", range = 3:7,      format = \"{:d}\",   startvalue = 3),\n",
    "        (label = \"k\", range = 0:8,      format = \"{:d}\",   startvalue = 1),\n",
    "        (label = \"β\", range = 0:.05:10, format = \"{:.2f}\", startvalue = .1),\n",
    "        width = 300,\n",
    "        halign = :left,\n",
    "        tellheight = false\n",
    "    )\n",
    "\n",
    "    slidervec = contents(fig[2, 1])\n",
    "    toggle_slidergrid!(slidervec, 1)\n",
    "\n",
    "    return slidervec\n",
    "\n",
    "end\n",
    "\n",
    "function updateNetwork!(ax, method, value)\n",
    "\n",
    "    @show method, value\n",
    "\n",
    "    if method == \"Erdos–Renyi\"\n",
    "        n, p = value[1:2]\n",
    "        g = erdos_renyi(n, p)\n",
    "    elseif method == \"Watts-Strogatz\"\n",
    "        n, k, β = value[3:end]\n",
    "        k = min(n - 1, k)\n",
    "        g = watts_strogatz(n, k, β)\n",
    "    end\n",
    "\n",
    "    println(Graphs.adjacency_matrix(g))\n",
    "    empty!(ax)\n",
    "    xlims!(ax, -1.1, 1.1)\n",
    "    ylims!(ax, -1.1, 1.1)\n",
    "    graphplot!(ax, g; layout = NetworkLayout.Shell())\n",
    "    Makie.hidedecorations!(ax)\n",
    "    Makie.hidespines!(ax)\n",
    "    ax.aspect = Makie.DataAspect()\n",
    "\n",
    "end\n",
    "\n",
    "fig = Figure(fontsize = 30, resolution=(1000, 1000),\n",
    "    figure_padding = (60, 5, 5, 5) # without padding the label left of the slider fall of the figure area and is hidden\n",
    ")\n",
    "\n",
    "menu = Menu(fig[1, 1],\n",
    "    options = [\"Erdos–Renyi\", \"Watts-Strogatz\"],\n",
    "    default = \"Erdos–Renyi\"\n",
    ")\n",
    "\n",
    "slidervec = initialize_sliders!(fig)\n",
    "\n",
    "on(menu.i_selected) do i\n",
    "    println(\"changing sliders\")\n",
    "    @show i\n",
    "    toggle_slidergrid!(slidervec, i)\n",
    "end\n",
    "\n",
    "ax = Axis(fig[1:2, 2])\n",
    "sliderobservables = [async_latest(s.value) for sg in slidervec for s in sg.sliders]\n",
    "\n",
    "map(menu.selection, sliderobservables...) do m, value...\n",
    "\n",
    "    println(\"changing plot\")\n",
    "    @show m, value\n",
    "    updateNetwork!(ax, m, value)\n",
    "\n",
    "end\n",
    "\n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "\n",
    "T = 10\n",
    "\n",
    "pts = range(-1, 1, length=100)\n",
    "ts = reshape(1:T, 1, 1, :)\n",
    "topo = cos.(pts) .+ cos.(ts .* pts')\n",
    "\n",
    "fig = Figure()\n",
    "ax = Axis(fig[1, 1])\n",
    "\n",
    "sg = SliderGrid(fig[2,1],\n",
    "    (label=\"time\", range=1:T))\n",
    "\n",
    "time = sg.sliders[1].value\n",
    "\n",
    "str = lift(t -> \"[$t ms]\", time)\n",
    "text!(ax, str)\n",
    "\n",
    "topo_slice = lift((t, data) -> data[:, :, t], time, topo)\n",
    "contour!(ax, topo_slice)\n",
    "\n",
    "hidedecorations!(ax)\n",
    "hidespines!(ax) \n",
    "fig\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "run (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using GLMakie\n",
    "\n",
    "function run(datadir::String, extension::String)\n",
    "\n",
    "\tfig = Figure()\n",
    "\tsc = display(fig)\n",
    "\n",
    "\t\n",
    "\txdata = [rand(10)]\n",
    "\tydata = [rand(10)]\n",
    "\txs = Observable(xdata[1])\n",
    "\tys = Observable(ydata[1])\n",
    "\tplotnames = Observable([\"nothing\"])\n",
    "\tplotname = Observable(plotnames[][1])\n",
    "\n",
    "\tmenu = Menu(fig, options = plotnames)\n",
    "\tfig[1, 1] = vgrid!(\n",
    "\t\t Label(fig, \"Data\", width = nothing), menu;\n",
    "\t\t tellheight = false, width = 200\n",
    "\t\t )\n",
    "\t\n",
    "\tax = Axis(fig[1, 2], title = plotname)\n",
    "\tline = lines!(ax, xs, ys)\n",
    "\n",
    "\ton(menu.selection) do s\n",
    "\t\t i = to_value(menu.i_selected)\n",
    "\t\t xs.val = xdata[i]\n",
    "\t\t ys[] = ydata[i]\n",
    "\t\t autolimits!(ax)\n",
    "\t\t ax.title = plotnames[][i]\n",
    "\tend\n",
    "\n",
    "\twhile true\n",
    "\t\t (file, event) = watch_folder(datadir)\n",
    "\n",
    "\t\t if endswith(file, extension)\n",
    "\n",
    "\t\t\t  x, y, name = loadfile(datadir, file, extension)\n",
    "\n",
    "\t\t\t  if !(name in plotnames[])\n",
    "\t\t\t\t\txs.val = x\n",
    "\t\t\t\t\tys[] = y\n",
    "\t\t\t\t\tautolimits!(ax)\n",
    "\t\t\t\t\tax.title = name\n",
    "\n",
    "\t\t\t\t\tpush!(xdata, x)\n",
    "\t\t\t\t\tpush!(ydata, y)\n",
    "\t\t\t\t\tplotnames[] = push!(plotnames[], name)\n",
    "\t\t\t  end\n",
    "\t\t end\n",
    "\tend\n",
    "\n",
    "end\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.1",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
